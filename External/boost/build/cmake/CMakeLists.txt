PROJECT(boost)

CMAKE_MINIMUM_REQUIRED(VERSION 2.8.6)

IF(COMMAND cmake_policy)
	CMAKE_POLICY(SET CMP0003 NEW)
ENDIF(COMMAND cmake_policy)

OPTION(WITH_ATOMIC "Build with atomic" OFF)
OPTION(WITH_CHRONO "Build with chrono" OFF)
OPTION(WITH_DATE_TIME "Build with date_time" OFF)
OPTION(WITH_FILESYSTEM "Build with filesystem" OFF)
OPTION(WITH_PROGRAM_OPTIONS "Build with program_options" OFF)
OPTION(WITH_REGEX "Build with regex" OFF)
OPTION(WITH_SYSTEM "Build with system" OFF)
OPTION(WITH_THREAD "Build with thread" OFF)

SET(BOOST_PROJECT_DIR "${CMAKE_HOME_DIRECTORY}/../../")

INCLUDE(${BOOST_PROJECT_DIR}/../../cmake/Common.cmake)
INCLUDE(${BOOST_PROJECT_DIR}/../../cmake/Platform.cmake)
INCLUDE(${BOOST_PROJECT_DIR}/../../cmake/Compiler.cmake)

SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS ON)

SET(BOOST_OUTPUT_DIR "${BOOST_PROJECT_DIR}/lib/${KLAYGE_PLATFORM_NAME}")
SET(KLAYGE_BIN_DIR "${BOOST_PROJECT_DIR}/../../KlayGE/bin/${KLAYGE_PLATFORM_NAME}")
SET(BOOST_VER "1_55")

IF(MSVC)
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} /W3 /WX-")
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W3 /WX-")
ENDIF()

IF(KLAYGE_BUILD_PLATFORM_WINDOWS_RUNTIME)
	ADD_DEFINITIONS(-DBOOST_NO_ANSI_APIS)
ENDIF()
IF(CMAKE_GENERATOR MATCHES "ARM")
	ADD_DEFINITIONS(-D_ARM_WINAPI_PARTITION_DESKTOP_SDK_AVAILABLE)
ENDIF()
IF(MSVC)
	ADD_DEFINITIONS(-D_CRT_SECURE_NO_DEPRECATE)
	ADD_DEFINITIONS(-D_SCL_SECURE_NO_DEPRECATE)
ENDIF()
IF(MINGW)
	IF(KLAYGE_ARCH_NAME STREQUAL "x64")
		ADD_DEFINITIONS(-DBOOST_USE_WINDOWS_H)
	ENDIF()
ENDIF()
IF(CMAKE_C_COMPILER_ID STREQUAL "Clang")
	ADD_DEFINITIONS(-DBOOST_USE_WINDOWS_H)
ENDIF()

MACRO(SETUP_BOOST_LIBRARY LIB_NAME)
	SOURCE_GROUP("Source Files" FILES ${SOURCE_FILES})

	IF(KLAYGE_PREFERRED_LIB_TYPE STREQUAL "SHARED")
		ADD_DEFINITIONS(-DBOOST_ALL_DYN_LINK=1)
	ELSE()
		ADD_DEFINITIONS(-DBOOST_All_STATIC_LINK=1)
	ENDIF()
	IF(KLAYGE_BUILD_PLATFORM_WINDOWS_RUNTIME)
		ADD_DEFINITIONS(-ZW)
	ENDIF()

	INCLUDE_DIRECTORIES(${BOOST_PROJECT_DIR}/)
	LINK_DIRECTORIES(${BOOST_PROJECT_DIR}/lib/${KLAYGE_PLATFORM_NAME})

	ADD_LIBRARY(${LIB_NAME} ${KLAYGE_PREFERRED_LIB_TYPE}
		${SOURCE_FILES}
	)

	SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES
		RUNTIME_OUTPUT_DIRECTORY ${BOOST_OUTPUT_DIR}
		RUNTIME_OUTPUT_DIRECTORY_DEBUG ${BOOST_OUTPUT_DIR}
		RUNTIME_OUTPUT_DIRECTORY_RELEASE ${BOOST_OUTPUT_DIR}
		RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${BOOST_OUTPUT_DIR}
		RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${BOOST_OUTPUT_DIR}
		ARCHIVE_OUTPUT_DIRECTORY ${BOOST_OUTPUT_DIR}
		ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${BOOST_OUTPUT_DIR}
		ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${BOOST_OUTPUT_DIR}
		ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO ${BOOST_OUTPUT_DIR}
		ARCHIVE_OUTPUT_DIRECTORY_MINSIZEREL ${BOOST_OUTPUT_DIR}
		PROJECT_LABEL ${LIB_NAME}
		DEBUG_POSTFIX ""
		DEBUG_OUTPUT_NAME boost_${LIB_NAME}-${KLAYGE_COMPILER_NAME}${KLAYGE_COMPILER_VERSION}-mt-gd-${BOOST_VER}
		RELEASE_OUTPUT_NAME boost_${LIB_NAME}-${KLAYGE_COMPILER_NAME}${KLAYGE_COMPILER_VERSION}-mt-${BOOST_VER}
		RELWITHDEBINFO_OUTPUT_NAME boost_${LIB_NAME}-${KLAYGE_COMPILER_NAME}${KLAYGE_COMPILER_VERSION}-mt-${BOOST_VER}
		MINSIZEREL_OUTPUT_NAME boost_${LIB_NAME}-${KLAYGE_COMPILER_NAME}${KLAYGE_COMPILER_VERSION}-mt-${BOOST_VER}
	)
	IF(KLAYGE_BUILD_PLATFORM_WINDOWS_RUNTIME)
		SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES VS_WINRT_EXTENSIONS TRUE)
	ENDIF()

	TARGET_LINK_LIBRARIES(${LIB_NAME})

	IF(KLAYGE_PREFERRED_LIB_TYPE STREQUAL "SHARED")
		INSTALL(TARGETS ${LIB_NAME}
			RUNTIME DESTINATION ${KLAYGE_BIN_DIR}
			LIBRARY DESTINATION ${KLAYGE_BIN_DIR}
			ARCHIVE DESTINATION ${KLAYGE_OUTPUT_DIR}
		)
	ENDIF()
ENDMACRO()

IF(WITH_ATOMIC)
	ADD_SUBDIRECTORY(atomic)
ENDIF()
IF(WITH_CHRONO)
	ADD_SUBDIRECTORY(chrono)
ENDIF()
IF(WITH_DATE_TIME)
	ADD_SUBDIRECTORY(date_time)
ENDIF()
IF(WITH_FILESYSTEM)
	ADD_SUBDIRECTORY(filesystem)
ENDIF()
IF(WITH_PROGRAM_OPTIONS)
	ADD_SUBDIRECTORY(program_options)
ENDIF()
IF(WITH_REGEX)
	ADD_SUBDIRECTORY(regex)
ENDIF()
IF(WITH_SYSTEM)
	ADD_SUBDIRECTORY(system)
ENDIF()
IF(WITH_THREAD)
	ADD_SUBDIRECTORY(thread)
ENDIF()
