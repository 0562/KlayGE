PROJECT(libvorbis)

CMAKE_MINIMUM_REQUIRED(VERSION 2.8.6)

IF(COMMAND cmake_policy)
	CMAKE_POLICY(SET CMP0003 NEW)
ENDIF(COMMAND cmake_policy)

SET(VORBIS_MAJOR_VERSION 1)
SET(VORBIS_MINOR_VERSION 3)
SET(VORBIS_PATCH_VERSION 3)
SET(VORBIS_VERSION ${VORBIS_MAJOR_VERSION}.${VORBIS_MINOR_VERSION}.${VORBIS_PATCH_VERSION})

FIND_PACKAGE(PythonInterp)

SET(VORBIS_PROJECT_DIR "${CMAKE_HOME_DIRECTORY}/../..")
SET(KLAYGE_ROOT_DIR "${VORBIS_PROJECT_DIR}/../../")

INCLUDE(${KLAYGE_ROOT_DIR}/cmake/Common.cmake)
INCLUDE(${KLAYGE_ROOT_DIR}/cmake/Platform.cmake)
INCLUDE(${KLAYGE_ROOT_DIR}/cmake/Compiler.cmake)

SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS ON)

SET(VORBIS_OUTPUT_DIR "${VORBIS_PROJECT_DIR}/libs/${KLAYGE_PLATFORM_NAME}")
SET(REL_PATH "External/Downloads/libvorbis-${VORBIS_VERSION}.tar.xz")
SET(DST_PATH "${KLAYGE_ROOT_DIR}/External/libvorbis/CHANGES")
SET(SEVENZIP_PATH "${KLAYGE_ROOT_DIR}/External/Downloads/${KLAYGE_PLATFORM_NAME}/7z")
SET(PATCH_PATH "${KLAYGE_ROOT_DIR}/External/Downloads/patch.py")

IF(KLAYGE_PLATFORM_WINDOWS)
	DOWNLOAD_FILE("External/Downloads/${KLAYGE_PLATFORM_NAME}/7z.exe")
	DOWNLOAD_FILE("External/Downloads/${KLAYGE_PLATFORM_NAME}/7z.dll")
ELSE()
	DOWNLOAD_FILE("External/Downloads/${KLAYGE_PLATFORM_NAME}/7z")
	DOWNLOAD_FILE("External/Downloads/${KLAYGE_PLATFORM_NAME}/7z.so")
	FILE(COPY ${SEVENZIP_PATH} DESTINATION "${KLAYGE_ROOT_DIR}/External/Downloads")
	FILE(COPY "${KLAYGE_ROOT_DIR}/External/Downloads/7z" DESTINATION "${KLAYGE_ROOT_DIR}/External/Downloads/${KLAYGE_PLATFORM_NAME}" FILE_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE)
ENDIF()
DOWNLOAD_FILE("External/Downloads/patch.py")
DOWNLOAD_FILE_URL(${REL_PATH} "http://downloads.xiph.org/releases/vorbis/libvorbis-${VORBIS_VERSION}.tar.xz")

IF(EXISTS ${DST_PATH})
	SET(VORBIS_VERSION_STR 0)
	FILE(STRINGS ${DST_PATH} VORBIS_VERSION_STR REGEX "libvorbis ")
	STRING(FIND "${VORBIS_VERSION_STR}" "libvorbis ${VORBIS_VERSION}" POS)
	IF(NOT POS EQUAL 0)
		FILE(REMOVE ${DST_PATH})
	ENDIF()
ENDIF()

ADD_CUSTOM_COMMAND(OUTPUT ${DST_PATH}
	COMMAND ${SEVENZIP_PATH} x -y ${KLAYGE_ROOT_DIR}/${REL_PATH}
	COMMAND ${SEVENZIP_PATH} x -y libvorbis-${VORBIS_VERSION}.tar
	COMMAND ${CMAKE_COMMAND} -E copy_directory libvorbis-${VORBIS_VERSION} .
	COMMAND ${CMAKE_COMMAND} -E remove libvorbis-${VORBIS_VERSION}.tar
	COMMAND ${CMAKE_COMMAND} -E remove_directory libvorbis-${VORBIS_VERSION}
	COMMAND ${PYTHON_EXECUTABLE} ${PATCH_PATH} -p0 Patches/ForMinGWAndWindowsARM.patch
	WORKING_DIRECTORY ${KLAYGE_ROOT_DIR}/External/libvorbis)
ADD_CUSTOM_TARGET(libvorbis_download ALL DEPENDS ${DST_PATH})

IF(MSVC)
	SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} /WX-")
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /WX-")
ENDIF()

ADD_SUBDIRECTORY(libvorbis)
ADD_SUBDIRECTORY(libvorbisfile)
