PROJECT(rapidxml)

CMAKE_MINIMUM_REQUIRED(VERSION 2.8.6)

IF(COMMAND cmake_policy)
	CMAKE_POLICY(SET CMP0003 NEW)
ENDIF(COMMAND cmake_policy)

SET(RAPIDXML_MAJOR_VERSION 1)
SET(RAPIDXML_MINOR_VERSION 13)
SET(RAPIDXML_VERSION ${RAPIDXML_MAJOR_VERSION}.${RAPIDXML_MINOR_VERSION})

FIND_PACKAGE(PythonInterp)

SET(RAPIDXML_DIR "${CMAKE_CURRENT_SOURCE_DIR}/../..")
SET(KLAYGE_ROOT_DIR "${RAPIDXML_DIR}/../..")

INCLUDE(${KLAYGE_ROOT_DIR}/cmake/Common.cmake)
INCLUDE(${KLAYGE_ROOT_DIR}/cmake/Platform.cmake)
INCLUDE(${KLAYGE_ROOT_DIR}/cmake/Compiler.cmake)

SET(KLAYGE_BIN_DIR "${RAPIDXML_DIR}/../../KlayGE/bin/${KLAYGE_PLATFORM_NAME}")
SET(REL_PATH "External/Downloads/rapidxml-${RAPIDXML_VERSION}.zip")
SET(DST_PATH "${KLAYGE_ROOT_DIR}/External/rapidxml/rapidxml.hpp")
SET(SEVENZIP_PATH "${KLAYGE_ROOT_DIR}/External/Downloads/${KLAYGE_PLATFORM_NAME}/7z")
SET(PATCH_PATH "${KLAYGE_ROOT_DIR}/External/Downloads/patch.py")

SET(REGEN FALSE)
IF(EXISTS ${DST_PATH})
	SET(RAPIDXML_VERSION_STR 0)
	FILE(STRINGS ${DST_PATH} RAPIDXML_VERSION_STR REGEX "// Version ")
	STRING(FIND "${RAPIDXML_VERSION_STR}" "// Version ${RAPIDXML_VERSION}" POS)
	IF(NOT POS EQUAL 0)
		SET(REGEN TRUE)
	ENDIF()
ELSE()
	SET(REGEN TRUE)
ENDIF()

IF(REGEN)
	IF(KLAYGE_PLATFORM_WINDOWS)
		DOWNLOAD_FILE("External/Downloads/${KLAYGE_PLATFORM_NAME}/7z.exe")
		DOWNLOAD_FILE("External/Downloads/${KLAYGE_PLATFORM_NAME}/7z.dll")
	ELSE()
		DOWNLOAD_FILE("External/Downloads/${KLAYGE_PLATFORM_NAME}/7z")
		DOWNLOAD_FILE("External/Downloads/${KLAYGE_PLATFORM_NAME}/7z.so")
		FILE(COPY ${SEVENZIP_PATH} DESTINATION "${KLAYGE_ROOT_DIR}/External/Downloads")
		FILE(COPY "${KLAYGE_ROOT_DIR}/External/Downloads/7z" DESTINATION "${KLAYGE_ROOT_DIR}/External/Downloads/${KLAYGE_PLATFORM_NAME}" FILE_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE)
	ENDIF()
	DOWNLOAD_FILE("External/Downloads/patch.py")
	DOWNLOAD_FILE_URL(${REL_PATH} "http://downloads.sourceforge.net/project/rapidxml/rapidxml/rapidxml%20${RAPIDXML_VERSION}/rapidxml-${RAPIDXML_VERSION}.zip")
	DOWNLOAD_FILE("External/rapidxml/Patches/ForGCC.patch")

	EXECUTE_PROCESS(COMMAND "${SEVENZIP_PATH}" "x" "-y" "${KLAYGE_ROOT_DIR}/${REL_PATH}" WORKING_DIRECTORY "${KLAYGE_ROOT_DIR}/External/rapidxml")
	FILE(COPY "${KLAYGE_ROOT_DIR}/External/rapidxml/rapidxml-${RAPIDXML_VERSION}/" DESTINATION "${KLAYGE_ROOT_DIR}/External/rapidxml/")
	FILE(REMOVE_RECURSE "${KLAYGE_ROOT_DIR}/External/rapidxml/rapidxml-${RAPIDXML_VERSION}")
	EXECUTE_PROCESS(COMMAND "${PYTHON_EXECUTABLE}" "${PATCH_PATH}" "-p0" "Patches/ForGCC.patch" WORKING_DIRECTORY "${KLAYGE_ROOT_DIR}/External/rapidxml")
ENDIF()
