CMAKE_MINIMUM_REQUIRED(VERSION 2.8.6)

PROJECT(Assimp)

SET(ASSIMP_PROJECT_DIR "${CMAKE_CURRENT_SOURCE_DIR}/../..")
SET(KLAYGE_ROOT_DIR "${ASSIMP_PROJECT_DIR}/../..")

INCLUDE(${KLAYGE_ROOT_DIR}/CMake/Common.cmake)
INCLUDE(${KLAYGE_ROOT_DIR}/CMake/Platform.cmake)
INCLUDE(${KLAYGE_ROOT_DIR}/CMake/Compiler.cmake)
INCLUDE(${KLAYGE_ROOT_DIR}/External/Build/CMake/ExternalCommon.cmake)

SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS ON)

SET(KLAYGE_BIN_DIR "${KLAYGE_ROOT_DIR}/KlayGE/bin/${KLAYGE_PLATFORM_NAME}")
IF(KLAYGE_PLATFORM_DARWIN OR KLAYGE_PLATFORM_LINUX)
	SET(ASSIMP_OUTPUT_DIR "${KLAYGE_BIN_DIR}")
ELSE()
	SET(ASSIMP_OUTPUT_DIR "${ASSIMP_PROJECT_DIR}/lib/${KLAYGE_PLATFORM_NAME}")
ENDIF()

UpdateExternalLib("assimp" "assimp" "git://github.com/assimp/assimp.git" "8c15a35609e8582945aec966f263d0ee821d63ae")

FOREACH(flag_var
		CMAKE_C_FLAGS CMAKE_CXX_FLAGS)
	IF(KLAYGE_COMPILER_MSVC)
		STRING(REPLACE "/W4" "/W0" ${flag_var} "${${flag_var}}")
		STRING(REPLACE "/WX" "/WX-" ${flag_var} "${${flag_var}}")
	ELSE()
		STRING(REPLACE "-Wall" "-w" ${flag_var} "${${flag_var}}")
		STRING(REPLACE "-Werror" "-Wno-error" ${flag_var} "${${flag_var}}")
	ENDIF()
ENDFOREACH()
FOREACH(flag_var
		CMAKE_CXX_FLAGS_RELEASE CMAKE_CXX_FLAGS_RELWITHDEBINFO CMAKE_CXX_FLAGS_MINSIZEREL)
	STRING(REPLACE "${NO_RTTI_FLAG}" "${RTTI_FLAG}" ${flag_var} "${${flag_var}}")
ENDFOREACH()

IF(MINGW)
	# To fix a problem that Importer/StepFile/StepFileGen1.cpp is too big
	SET(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -O1")
ENDIF()

OPTION(BUILD_SHARED_LIBS "" ON)
OPTION(BUILD_TESTING "" OFF)
OPTION(ASSIMP_OPT_BUILD_PACKAGES "" OFF)
OPTION(ASSIMP_ANDROID_JNIIOSYSTEM "" OFF)
OPTION(ASSIMP_NO_EXPORT "" OFF)
OPTION(ASSIMP_BUILD_ASSIMP_TOOLS "" OFF)
OPTION(ASSIMP_BUILD_NONFREE_C4D_IMPORTER "" OFF)
OPTION(ASSIMP_BUILD_SAMPLES "" OFF)
OPTION(ASSIMP_BUILD_TESTS "" OFF)
OPTION(ASSIMP_BUILD_ZLIB "" OFF)
OPTION(ASSIMP_INSTALL_PDB "" OFF)
OPTION(IGNORE_GIT_HASH "" ON)

ADD_SUBDIRECTORY(${ASSIMP_PROJECT_DIR}/assimp "${CMAKE_CURRENT_BINARY_DIR}/assimp")

SET_TARGET_PROPERTIES(assimp PROPERTIES
	ARCHIVE_OUTPUT_DIRECTORY ${ASSIMP_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${ASSIMP_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${ASSIMP_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO ${ASSIMP_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_MINSIZEREL ${ASSIMP_OUTPUT_DIR}
	RUNTIME_OUTPUT_DIRECTORY ${KLAYGE_BIN_DIR}
	RUNTIME_OUTPUT_DIRECTORY_DEBUG ${KLAYGE_BIN_DIR}
	RUNTIME_OUTPUT_DIRECTORY_RELEASE ${KLAYGE_BIN_DIR}
	RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO ${KLAYGE_BIN_DIR}
	RUNTIME_OUTPUT_DIRECTORY_MINSIZEREL ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY_DEBUG ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY_RELEASE ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY_RELWITHDEBINFO ${KLAYGE_BIN_DIR}
	LIBRARY_OUTPUT_DIRECTORY_MINSIZEREL ${KLAYGE_BIN_DIR}
	DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX}
	OUTPUT_NAME assimp${KLAYGE_OUTPUT_SUFFIX}
)
