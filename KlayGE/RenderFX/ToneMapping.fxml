<?xml version='1.0'?>

<effect>
	<include name="PostProcess.fxml"/>

	<parameter type="sampler" name="linear_sampler">
		<state name="filtering" value="min_mag_linear_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>

	<parameter type="sampler" name="point_sampler">
		<state name="filtering" value="min_mag_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>

	<parameter type="texture2D" name="src_tex"/>
	<parameter type="texture2D" name="lum_tex"/>
	<parameter type="texture2D" name="bloom_tex"/>

	<shader>
		<![CDATA[
float4 ToneMapping(float3 color, float3 blur, float adapted_lum)
{
	const float MIDDLE_GREY = 0.36f;
	const half3 RGB_TO_LUM = half3(0.27, 0.67, 0.06);
	const half3 BLUE_SHIFT = half3(1.05, 0.97, 1.27); 

	color += blur * 0.25f;

	// Blue shift
	half blue_shift_coef = 1.0f - (adapted_lum + 1.5f) / 4.1f;
	half3 rod_clr = dot(color, RGB_TO_LUM) * BLUE_SHIFT;
	color = lerp(color, rod_clr, saturate(blue_shift_coef));

	// Tone mapping
	color *= MIDDLE_GREY / adapted_lum;
	color /= 1.0f + color;

	return float4(color, 1);
}

void ToneMapping30VS(float4 pos : POSITION,
					out float3 oTex : TEXCOORD0,
					out float4 oPos : SV_Position)
{
	oPos = AdjustPos(pos);

	oTex.xy = TexCoordFromPos(pos);
	oTex.z = max(0.001f, lum_tex.SampleLevel(point_sampler, 0.5f.xx, 0).r);
}

float4 ToneMapping30PS(float3 iTex : TEXCOORD0) : SV_Target
{
	return ToneMapping(src_tex.Sample(linear_sampler, iTex.xy), bloom_tex.Sample(linear_sampler, iTex.xy), iTex.z);
}

float4 ToneMapping20PS(float2 iTex : TEXCOORD0) : SV_Target
{
	half lum = max(0.001f, lum_tex.Sample(point_sampler, 0.5f).r);
	return ToneMapping(src_tex.Sample(linear_sampler, iTex), bloom_tex.Sample(linear_sampler, iTex), lum);
}
		]]>
	</shader>
	
	<technique name="ToneMapping30">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="ToneMapping30VS()"/>
			<state name="pixel_shader" value="ToneMapping30PS()"/>
		</pass>
	</technique>
	
	<technique name="ToneMapping20">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="PostProcessVS()"/>
			<state name="pixel_shader" value="ToneMapping20PS()"/>
		</pass>
	</technique>
</effect>
