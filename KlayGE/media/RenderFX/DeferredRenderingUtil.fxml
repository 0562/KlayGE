<?xml version='1.0'?>

<effect>
	<shader>
		<![CDATA[
float4 StoreGBufferRT0(float3 normal, float shininess)
{
	float p = sqrt(-normal.z * 8 + 8);
	float2 enc = normal.xy / p + 0.5f;
	float2 enc255 = enc * 255;
	float2 residual = floor(frac(enc255) * 16);
	return float4(float3(floor(enc255), residual.x * 16 + residual.y) / 255, shininess);
}

float4 StoreGBufferRT1(float3 diffuse, float3 specular)
{
	return float4(diffuse, dot(specular, float3(0.2126f, 0.7152f, 0.0722f)));
}

void StoreGBufferMRT(float3 normal, float3 specular, float shininess, float3 diffuse,
			out float4 mrt_0, out float4 mrt_1)
{
	mrt_0 = StoreGBufferRT0(normal, shininess);
	mrt_1 = StoreGBufferRT1(diffuse, specular);
}

float3 GetNormal(float4 mrt0)
{
	float nz = floor(mrt0.z * 255) / 16;
	mrt0.xy += float2(floor(nz) / 16, frac(nz)) / 255;
	float2 fenc = mrt0.xy * 4 - 2;
	float f = dot(fenc, fenc);
	float g = sqrt(1 - f / 4);
	float3 normal;
	normal.xy = fenc * g;
	normal.z = f / 2 - 1;
	return normal;
}

float GetShininess(float4 mrt0)
{
	return mrt0.w * 256.0f;
}

float3 GetDiffuse(float4 mrt1)
{
	return mrt1.xyz;
}

float GetSpecular(float4 mrt1)
{
	return mrt1.w;
}


float3 NearestDepthUpsampling(float2 tc, float4 z_half, float z_full, float2 l1_inv_width_height, float threshold)
{
	float4 dist_z = abs(z_half - z_full);
	float nearest_z = dist_z.x;
	float2 nearest_tc = tc;
	if (dist_z.y < nearest_z)
	{
		nearest_z = dist_z.y;
		nearest_tc = tc + float2(l1_inv_width_height.x, 0);
	}
	if (dist_z.z < nearest_z)
	{
		nearest_z = dist_z.z;
		nearest_tc = tc + float2(0, l1_inv_width_height.y);
	}
	if (dist_z.w < nearest_z)
	{
		nearest_z = dist_z.w;
		nearest_tc = tc + l1_inv_width_height;
	}

	return float3(nearest_tc, all(dist_z < threshold) ? 1 : -1);
}

float3 FittingCoord(float3 normal)
{
	float3 normal_uns = abs(normal);
	float max_comp = max(normal_uns.z, max(normal_uns.x, normal_uns.y));
	float2 tc = normal_uns.z < max_comp ? (normal_uns.y < max_comp ? normal_uns.yz : normal_uns.xz) : normal_uns.xy;
	tc = tc.x < tc.y ? tc.yx : tc.xy;
	tc.y /= tc.x;
	return float3(tc, 1 / max_comp);
}
		]]>
	</shader>
</effect>
