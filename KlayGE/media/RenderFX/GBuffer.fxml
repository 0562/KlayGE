<?xml version='1.0'?>

<effect>
	<include name="DeferredRenderingUtil.fxml"/>
	<include name="Lighting.fxml"/>
	<include name="util.fxml"/>

	<macro name="ALPHA_TEST_THRESHOLD" value="0.2"/>
	
	<cbuffer name="per_frame">
		<parameter type="float4x4" name="mvp"/>
		<parameter type="float4x4" name="model_view"/>
		<parameter type="float4x4" name="inv_mvp"/>

		<parameter type="float3" name="depth_near_far_invfar"/>
	</cbuffer>

	<cbuffer name="model">
		<parameter type="float4" name="diffuse_clr"/>
		<parameter type="float4" name="emit_clr"/>
		<parameter type="float4" name="specular_level"/>
		<parameter type="float" name="shininess"/>
		<parameter type="float" name="opacity_clr"/>
		<parameter type="int" name="normal_map_enabled"/>
		<parameter type="int" name="height_map_enabled"/>
		<parameter type="int" name="opacity_map_enabled"/>
	</cbuffer>

	<parameter type="texture2D" name="diffuse_tex"/>
	<parameter type="texture2D" name="specular_tex"/>
	<parameter type="texture2D" name="emit_tex"/>
	<parameter type="texture2D" name="normal_tex"/>
	<parameter type="texture2D" name="height_tex"/>

	<parameter type="sampler" name="bilinear_sampler">
		<state name="filtering" value="min_mag_linear_mip_point"/>
		<state name="address_u" value="wrap"/>
		<state name="address_v" value="wrap"/>
	</parameter>
	<parameter type="sampler" name="trilinear_sampler">
		<state name="filtering" value="min_mag_mip_linear"/>
		<state name="address_u" value="wrap"/>
		<state name="address_v" value="wrap"/>
	</parameter>
	<parameter type="sampler" name="aniso_sampler">
		<state name="filtering" value="anisotropic"/>
		<state name="address_u" value="wrap"/>
		<state name="address_v" value="wrap"/>
		<state name="max_anisotropy" value="4"/>
	</parameter>

	<parameter type="sampler" name="skybox_sampler">
		<state name="filtering" value="min_mag_linear_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
		<state name="address_w" value="clamp"/>
	</parameter>

	<shader>
		<![CDATA[
void GBufferVS(float4 pos : POSITION,
			float2 texcoord : TEXCOORD0,
			float3 normal : NORMAL,
			float4 tangent : TANGENT,
			out float4 oTexCoord_2xy : TEXCOORD0,
			out float4 oTsToView0_2z : TEXCOORD1,
			out float3 oTsToView1 : TEXCOORD2,
			out float4 oPos : SV_Position)
{
	oPos = mul(pos, mvp);
	oTexCoord_2xy.xy = texcoord;

	normal = normal * 2 - 1;
	tangent = tangent * 2 - 1;

	float3x3 obj_to_ts;
	obj_to_ts[0] = tangent.xyz;
	obj_to_ts[1] = cross(normal, tangent.xyz) * tangent.w;
	obj_to_ts[2] = normal;
	float3x3 ts_to_view = mul(obj_to_ts, (float3x3)model_view);
	oTsToView0_2z.xyz = ts_to_view[0];
	oTsToView1 = ts_to_view[1];
	oTexCoord_2xy.zw = ts_to_view[2].xy;
	oTsToView0_2z.w = ts_to_view[2].z;
}

void RestoreNormalSpecular(float4 texcoord_2xy, float4 ts_to_view0_2z, float3 ts_to_view1,
	out float3 normal, out float specular)
{
	normal = normalize(float3(texcoord_2xy.zw, ts_to_view0_2z.w));
	if (normal_map_enabled)
	{
		float3x3 ts_to_view;
		ts_to_view[0] = normalize(ts_to_view0_2z.xyz);
		ts_to_view[1] = normalize(ts_to_view1);
		ts_to_view[2] = normal;
		normal = normalize(mul(decompress_normal(normal_tex.Sample(trilinear_sampler, texcoord_2xy.xy)), ts_to_view));
	}

	if (specular_level.w > 0.5f)
	{
		specular = dot(specular_tex.Sample(aniso_sampler, texcoord_2xy.xy).rgb, float3(0.2126f, 0.7152f, 0.0722f));
	}
	else
	{
		specular = specular_level.x;
	}
}

float4 ConstructGBuffer(float4 texcoord_2xy, float4 ts_to_view0_2z, float3 ts_to_view1)
{
	float3 normal;
	float specular;
	RestoreNormalSpecular(texcoord_2xy, ts_to_view0_2z, ts_to_view1,
			normal, specular);

	return StoreGBuffer(normal, specular, shininess);
}

void ConstructMRTGBuffer(float4 texcoord_2xy, float4 ts_to_view0_2z, float3 ts_to_view1, float opacity,
					out float4 mrt_0, out float4 mrt_1)
{
	float3 normal;
	float specular;
	RestoreNormalSpecular(texcoord_2xy, ts_to_view0_2z, ts_to_view1,
			normal, specular);

	float3 diffuse;
	if (diffuse_clr.w > 0.5f)
	{
		diffuse = diffuse_tex.Sample(aniso_sampler, texcoord_2xy.xy).rgb;
	}
	else
	{
		diffuse = diffuse_clr.rgb;
	}
	
	StoreMRTGBuffer(normal, specular, shininess, diffuse, opacity,
		mrt_0, mrt_1);
}

float4 GBufferPS(float4 texcoord_2xy : TEXCOORD0, float4 ts_to_view0_2z : TEXCOORD1, float3 ts_to_view1 : TEXCOORD2) : SV_Target
{
	return ConstructGBuffer(texcoord_2xy, ts_to_view0_2z, ts_to_view1);
}

float4 GBufferAlphaTestPS(float4 texcoord_2xy : TEXCOORD0, float4 ts_to_view0_2z : TEXCOORD1, float3 ts_to_view1 : TEXCOORD2) : SV_Target
{
	clip(diffuse_tex.Sample(bilinear_sampler, texcoord_2xy.xy).a - ALPHA_TEST_THRESHOLD);
	return ConstructGBuffer(texcoord_2xy, ts_to_view0_2z, ts_to_view1);
}

void GBufferMRTPS(float4 texcoord_2xy : TEXCOORD0, float4 ts_to_view0_2z : TEXCOORD1, float3 ts_to_view1 : TEXCOORD2,
					out float4 mrt_0 : SV_Target0, out float4 mrt_1 : SV_Target1)
{
	ConstructMRTGBuffer(texcoord_2xy, ts_to_view0_2z, ts_to_view1, 1, mrt_0, mrt_1);
}

void GBufferAlphaTestMRTPS(float4 texcoord_2xy : TEXCOORD0, float4 ts_to_view0_2z : TEXCOORD1, float3 ts_to_view1 : TEXCOORD2,
					out float4 mrt_0 : SV_Target0, out float4 mrt_1 : SV_Target1)
{
	clip(diffuse_tex.Sample(bilinear_sampler, texcoord_2xy.xy).a - ALPHA_TEST_THRESHOLD);
	ConstructMRTGBuffer(texcoord_2xy, ts_to_view0_2z, ts_to_view1, 1, mrt_0, mrt_1);
}

void GBufferAlphaBlendMRTPS(float4 texcoord_2xy : TEXCOORD0, float4 ts_to_view0_2z : TEXCOORD1, float3 ts_to_view1 : TEXCOORD2,
					out float4 mrt_0 : SV_Target0, out float4 mrt_1 : SV_Target1)
{
	float opacity;
	if (opacity_map_enabled)
	{
		opacity = diffuse_tex.Sample(aniso_sampler, texcoord_2xy.xy).a;
	}
	else
	{
		opacity = opacity_clr;
	}
	ConstructMRTGBuffer(texcoord_2xy, ts_to_view0_2z, ts_to_view1, opacity, mrt_0, mrt_1);
}

void GBufferSkyBoxVS(float4 pos : POSITION,
			out float4 oPos : SV_Position)
{
	oPos = pos;
}

float4 GBufferSkyBoxPS() : SV_Target
{
	return 0;
}

void GBufferSkyBoxMRTPS(out float4 mrt_0 : SV_Target0, out float4 mrt_1 : SV_Target1)
{
	mrt_0 = mrt_1 = 0;
}
		]]>
	</shader>

	<technique name="GBufferTech">
		<pass name="p0">
			<state name="cull_mode" value="back"/>

			<state name="vertex_shader" value="GBufferVS()"/>
			<state name="pixel_shader" value="GBufferPS()"/>
		</pass>
	</technique>
	<technique name="GBufferAlphaTestTech" inherit="GBufferTech">
		<pass name="p0">
			<state name="pixel_shader" value="GBufferAlphaTestPS()"/>
		</pass>
	</technique>
	<technique name="GBufferAlphaBlendBackTech">
		<pass name="p0">
			<state name="depth_func" value="greater"/>
			
			<state name="cull_mode" value="front"/>

			<state name="vertex_shader" value="GBufferVS()"/>
			<state name="pixel_shader" value="GBufferAlphaTestPS()"/>
		</pass>
	</technique>
	<technique name="GBufferAlphaBlendFrontTech" inherit="GBufferAlphaBlendBackTech">
		<pass name="p0">
			<state name="depth_func" value="less"/>
			
			<state name="cull_mode" value="back"/>
		</pass>
	</technique>

	<technique name="GBufferMRTTech">
		<pass name="p0">
			<state name="cull_mode" value="back"/>

			<state name="vertex_shader" value="GBufferVS()"/>
			<state name="pixel_shader" value="GBufferMRTPS()"/>
		</pass>
	</technique>
	<technique name="GBufferAlphaTestMRTTech" inherit="GBufferMRTTech">
		<pass name="p0">
			<state name="pixel_shader" value="GBufferAlphaTestMRTPS()"/>
		</pass>
	</technique>
	<technique name="GBufferAlphaBlendBackMRTTech">
		<pass name="p0">
			<state name="depth_func" value="greater"/>
			
			<state name="cull_mode" value="front"/>

			<state name="front_stencil_enable" value="true"/>
			<state name="front_stencil_func" value="always_pass"/>
			<state name="front_stencil_ref" value="0"/>
			<state name="front_stencil_pass" value="replace"/>
			<state name="back_stencil_enable" value="true"/>
			<state name="back_stencil_func" value="always_pass"/>
			<state name="back_stencil_ref" value="0"/>
			<state name="back_stencil_pass" value="replace"/>

			<state name="vertex_shader" value="GBufferVS()"/>
			<state name="pixel_shader" value="GBufferAlphaBlendMRTPS()"/>
		</pass>
	</technique>
	<technique name="GBufferAlphaBlendFrontMRTTech" inherit="GBufferAlphaBlendBackMRTTech">
		<pass name="p0">
			<state name="depth_func" value="less"/>
			
			<state name="cull_mode" value="back"/>
		</pass>
	</technique>

	<technique name="GBufferSkyBoxTech">
		<pass name="p0">
			<state name="cull_mode" value="none"/>
			<state name="depth_func" value="equal"/>
			<state name="depth_write_mask" value="false"/>

			<state name="front_stencil_enable" value="true"/>
			<state name="front_stencil_func" value="always_pass"/>
			<state name="front_stencil_ref" value="128"/>
			<state name="front_stencil_pass" value="replace"/>
			<state name="back_stencil_enable" value="true"/>
			<state name="back_stencil_func" value="always_pass"/>
			<state name="back_stencil_ref" value="128"/>
			<state name="back_stencil_pass" value="replace"/>

			<state name="vertex_shader" value="GBufferSkyBoxVS()"/>
			<state name="pixel_shader" value="GBufferSkyBoxPS()"/>
		</pass>
	</technique>

	<technique name="GBufferSkyBoxMRTTech" inherit="GBufferSkyBoxTech">
		<pass name="p0">
			<state name="pixel_shader" value="GBufferSkyBoxMRTPS()"/>
		</pass>
	</technique>

	<shader>
		<![CDATA[
void GenReflectiveShadowMapVS(float4 pos : POSITION,
						float2 texcoord : TEXCOORD0,
						float3 normal   : NORMAL,
						float4 tangent : TANGENT,
						out float4 oTexCoord_2xy : TEXCOORD0,
						out float4 oTsToView0_2z : TEXCOORD1,
						out float3 oTsToView1 : TEXCOORD2,
						out float4 oPos : SV_Position)
{
	oPos = mul(pos, mvp);
	oTexCoord_2xy.xy = texcoord;

	normal = normal * 2 - 1;
	tangent = tangent * 2 - 1;

	float3x3 obj_to_ts;
	obj_to_ts[0] = tangent.xyz;
	obj_to_ts[1] = cross(normal, tangent.xyz) * tangent.w;
	obj_to_ts[2] = normal;
	float3x3 ts_to_view = mul(obj_to_ts, (float3x3)model_view);
	oTsToView0_2z.xyz = ts_to_view[0];
	oTsToView1 = ts_to_view[1];
	oTexCoord_2xy.zw = ts_to_view[2].xy;
	oTsToView0_2z.w = ts_to_view[2].z;
}

void ConstructRSMBuffer(float4 texcoord_2xy, float4 ts_to_view0_2z, float3 ts_to_view1,
						out float4 oAlbedo, out float4 oNormal)
{
	if (diffuse_clr.w > 0.5f)
	{
		oAlbedo.xyz = diffuse_tex.Sample(aniso_sampler, texcoord_2xy.xy).xyz;
	}
	else
	{
		oAlbedo.xyz = diffuse_clr.xyz;
	}
	if (specular_level.w > 0.5f)
	{
		oAlbedo.w = dot(specular_tex.Sample(aniso_sampler, texcoord_2xy.xy).rgb, float3(0.2126f, 0.7152f, 0.0722f));
	}
	else
	{
		oAlbedo.w = specular_level.x;
	}

	float3 normal = normalize(float3(texcoord_2xy.zw, ts_to_view0_2z.w));
	if (normal_map_enabled)
	{
		float3x3 ts_to_view;
		ts_to_view[0] = normalize(ts_to_view0_2z.xyz);
		ts_to_view[1] = normalize(ts_to_view1);
		ts_to_view[2] = normal;
		normal = normalize(mul(decompress_normal(normal_tex.Sample(trilinear_sampler, texcoord_2xy.xy)), ts_to_view));
	}

	oNormal = float4(normal / 2 + 0.5f, shininess);
}

void GenReflectiveShadowMapPS(float4 texcoord_2xy : TEXCOORD0, float4 ts_to_view0_2z : TEXCOORD1, float3 ts_to_view1 : TEXCOORD2,
						out float4 oAlbedo : SV_Target0, 
						out float4 oNormal : SV_Target1)
{
	ConstructRSMBuffer(texcoord_2xy, ts_to_view0_2z, ts_to_view1, oAlbedo, oNormal);
}

void GenReflectiveShadowMapAlphaTestPS(float4 texcoord_2xy : TEXCOORD0, float4 ts_to_view0_2z : TEXCOORD1, float3 ts_to_view1 : TEXCOORD2,
						out float4 oAlbedo : SV_Target0, 
						out float4 oNormal : SV_Target1)
{
	clip(diffuse_tex.Sample(bilinear_sampler, texcoord_2xy.xy).a - ALPHA_TEST_THRESHOLD);
	ConstructRSMBuffer(texcoord_2xy, ts_to_view0_2z, ts_to_view1, oAlbedo, oNormal);
}
		]]>
	</shader>

	<technique name="GenReflectiveShadowMapTech">
		<pass name="p0">
			<state name="cull_mode" value="back"/>

			<state name="vertex_shader" value="GenReflectiveShadowMapVS()"/>
			<state name="pixel_shader" value="GenReflectiveShadowMapPS()"/>
		</pass>
	</technique>

	<technique name="GenReflectiveShadowMapAlphaTestTech" inherit="GenReflectiveShadowMapTech">
		<pass name="p0">
			<state name="pixel_shader" value="GenReflectiveShadowMapAlphaTestPS()"/>
		</pass>
	</technique>

	<shader>
		<![CDATA[
void GenShadowMapVS(float4 pos : POSITION,
						float2 texcoord : TEXCOORD0,
						out float2 oTc : TEXCOORD0,
						out float4 oPos : SV_Position)
{
	oPos = mul(pos, mvp);
	oTc = texcoord;
}

float4 GenShadowMapPS(float2 tc : TEXCOORD0) : SV_Target
{
	return 0;
}

float4 GenShadowMapAlphaTestPS(float2 tc : TEXCOORD0) : SV_Target
{
	clip(diffuse_tex.Sample(bilinear_sampler, tc).a - ALPHA_TEST_THRESHOLD);	
	return 0;
}
		]]>
	</shader>

	<technique name="GenShadowMapTech">
		<pass name="p0">
			<state name="cull_mode" value="none"/>
			<state name="color_write_mask" value="0"/>
			
			<state name="vertex_shader" value="GenShadowMapVS()"/>
			<state name="pixel_shader" value="GenShadowMapPS()"/>
		</pass>
	</technique>
	<technique name="GenShadowMapAlphaTestTech" inherit="GenShadowMapTech">
		<pass name="p0">
			<state name="pixel_shader" value="GenShadowMapAlphaTestPS()"/>
		</pass>
	</technique>


	<parameter type="textureCUBE" name="skybox_tex"/>
	<parameter type="textureCUBE" name="skybox_C_tex"/>

	<parameter type="texture2D" name="lighting_tex"/>

	<parameter type="sampler" name="point_sampler">
		<state name="filtering" value="min_mag_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>

	<cbuffer name="per_frame2">
		<parameter type='int' name='flipping'/>
	</cbuffer>
	
	<shader>
		<![CDATA[
#ifdef KLAYGE_D3D11
#define NOPERSPECTIVE_SUPPORT
#endif

void ShadingVS(float4 pos : POSITION,
			float2 texcoord : TEXCOORD0,
			out float2 oTexCoord : TEXCOORD0,
#ifdef NOPERSPECTIVE_SUPPORT
			out noperspective float2 oTc : TEXCOORD1,
#else
			out float3 oTc : TEXCOORD1,
#endif
			out float4 oPos : SV_Position)
{
	oPos = mul(pos, mvp);
	oTexCoord = texcoord;

	oTc.xy = oPos.xy / oPos.w;
	oTc.y *= flipping;
	oTc.xy = oTc.xy * 0.5f + 0.5f;

#ifndef NOPERSPECTIVE_SUPPORT
	oTc.z = oPos.w;
	oTc.xy *= oTc.z;
#endif
}

float4 CalcShading(float2 tc, float3 diffuse, float3 emit, float opacity)
{	
	float4 lighting = lighting_tex.Sample(point_sampler, tc);
	float3 diff_lighting = lighting.rgb;
	float3 spec_lighting = lighting.a / (dot(lighting.rgb, float3(0.2126f, 0.7152f, 0.0722f)) + 1e-6f) * lighting.rgb;
	return float4(max(emit + diff_lighting * diffuse + specular_normalize_factor(shininess) * spec_lighting, 0), opacity);
}

float4 ShadingPS(float2 texcoord : TEXCOORD0,
#ifdef NOPERSPECTIVE_SUPPORT
	noperspective float2 tc : TEXCOORD1
#else
	float3 tc_w : TEXCOORD1
#endif
	) : SV_Target
{
#ifndef NOPERSPECTIVE_SUPPORT
	float2 tc = tc_w.xy / tc_w.z;
#endif

	float3 diffuse;
	if (diffuse_clr.w > 0.5f)
	{
		diffuse = diffuse_tex.Sample(aniso_sampler, texcoord).rgb;
	}
	else
	{
		diffuse = diffuse_clr.rgb;
	}
	float3 emit;
	if (emit_clr.w > 0.5f)
	{
		emit = emit_tex.Sample(aniso_sampler, texcoord).rgb;
	}
	else
	{
		emit = emit_clr.rgb;
	}
	return CalcShading(tc, diffuse, emit, 1);
}

float4 ShadingAlphaBlendPS(float2 texcoord : TEXCOORD0,
#ifdef NOPERSPECTIVE_SUPPORT
	noperspective float2 tc : TEXCOORD1
#else
	float3 tc_w : TEXCOORD1
#endif
	) : SV_Target
{
#ifndef NOPERSPECTIVE_SUPPORT
	float2 tc = tc_w.xy / tc_w.z;
#endif

	float3 diffuse;
	if (diffuse_clr.w > 0.5f)
	{
		diffuse = diffuse_tex.Sample(aniso_sampler, texcoord).rgb;
	}
	else
	{
		diffuse = diffuse_clr.rgb;
	}
	float3 emit;
	if (emit_clr.w > 0.5f)
	{
		emit = emit_tex.Sample(aniso_sampler, texcoord).rgb;
	}
	else
	{
		emit = emit_clr.rgb;
	}
	float opacity;
	if (opacity_map_enabled)
	{
		opacity = diffuse_tex.Sample(aniso_sampler, texcoord).a;
	}
	else
	{
		opacity = opacity_clr;
	}
	return CalcShading(tc, diffuse, emit, opacity);
}

float4 SpecialShadingPS(float2 texcoord : TEXCOORD0,
#ifdef NOPERSPECTIVE_SUPPORT
	noperspective float2 tc : TEXCOORD1
#else
	float3 tc_w : TEXCOORD1
#endif
	) : SV_Target
{
#ifndef NOPERSPECTIVE_SUPPORT
	float2 tc = tc_w.xy / tc_w.z;
#endif

	float3 emit;
	if (emit_clr.w > 0.5f)
	{
		emit = emit_tex.Sample(aniso_sampler, texcoord).rgb;
	}
	else
	{
		emit = emit_clr.rgb;
	}
	return float4(max(emit, 0), 0);
}

void ShadingSkyBoxVS(float4 pos : POSITION,
			out float3 texcoord0 : TEXCOORD0,
			out float4 oPos : SV_Position)
{
	oPos = pos;
	texcoord0 = mul(pos, inv_mvp).xyz;
}

float4 ShadingSkyBoxPS(float3 texcoord : TEXCOORD0) : SV_Target
{
	return decode_hdr_yc(skybox_tex.Sample(skybox_sampler, texcoord).r,
					skybox_C_tex.Sample(skybox_sampler, texcoord));
}
		]]>
	</shader>

	<technique name="ShadingTech">
		<pass name="p0">
			<state name="cull_mode" value="back"/>
			<state name="depth_enable" value="true"/>
			<state name="depth_func" value="equal"/>
			<state name="depth_write_mask" value="false"/>

			<state name="vertex_shader" value="ShadingVS()"/>
			<state name="pixel_shader" value="ShadingPS()"/>
		</pass>
	</technique>

	<technique name="ShadingAlphaBlendBackTech">
		<pass name="p0">
			<state name="cull_mode" value="front"/>
			<state name="depth_enable" value="true"/>
			<state name="depth_func" value="equal"/>
			<state name="depth_write_mask" value="false"/>

			<state name="vertex_shader" value="ShadingVS()"/>
			<state name="pixel_shader" value="ShadingAlphaBlendPS()"/>
		</pass>
	</technique>
	<technique name="ShadingAlphaBlendFrontTech">
		<pass name="p0">
			<state name="cull_mode" value="back"/>
			<state name="depth_enable" value="true"/>
			<state name="depth_func" value="equal"/>
			<state name="depth_write_mask" value="false"/>

			<state name="vertex_shader" value="ShadingVS()"/>
			<state name="pixel_shader" value="ShadingAlphaBlendPS()"/>
		</pass>
	</technique>

	<technique name="ShadingSkyBoxTech">
		<pass name="p0">
			<state name="cull_mode" value="none"/>
			<state name="depth_enable" value="true"/>
			<state name="depth_func" value="equal"/>
			<state name="depth_write_mask" value="false"/>

			<state name="vertex_shader" value="ShadingSkyBoxVS()"/>
			<state name="pixel_shader" value="ShadingSkyBoxPS()"/>
		</pass>
	</technique>

	<technique name="SpecialShadingTech">
		<pass name="p0">
			<state name="cull_mode" value="back"/>
			<state name="depth_enable" value="true"/>
			<state name="depth_func" value="equal"/>
			<state name="depth_write_mask" value="false"/>

			<state name="blend_enable" value="true"/>
			<state name="blend_op" value="add"/>
			<state name="src_blend" value="one"/>
			<state name="dest_blend" value="one"/>
			<state name="blend_op_alpha" value="add"/>
			<state name="src_blend_alpha" value="one"/>
			<state name="dest_blend_alpha" value="one"/>

			<state name="vertex_shader" value="ShadingVS()"/>
			<state name="pixel_shader" value="SpecialShadingPS()"/>
		</pass>
	</technique>
</effect>
