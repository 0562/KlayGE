IF(KLAYGE_PLATFORM_WINDOWS_DESKTOP)
	SET(KLAYGE_LibOVR_PATH "" CACHE STRING "Path of LibOVR")
	IF(KLAYGE_LibOVR_PATH)
		SET(KLAYGE_LIBOVR_FOUND TRUE)
	ENDIF()
ENDIF()

SET(LIB_NAME KlayGE_InputEngine_MsgInput)

SET(MSGINPUT_IE_SOURCE_FILES
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MIJoystick.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MIKeyboard.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MIMouse.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MInputEngine.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MInputFactory.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MISensor.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MITouch.cpp
	${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MIOVR.cpp
)

SET(MSGINPUT_IE_HEADER_FILES
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/MsgInput/MInput.hpp
	${KLAYGE_PROJECT_DIR}/Plugins/Include/KlayGE/MsgInput/MInputFactory.hpp
)

SOURCE_GROUP("Source Files" FILES ${MSGINPUT_IE_SOURCE_FILES})
SOURCE_GROUP("Header Files" FILES ${MSGINPUT_IE_HEADER_FILES})

ADD_DEFINITIONS(-DKLAYGE_BUILD_DLL -DKLAYGE_MSGINPUT_IE_SOURCE)
IF(KLAYGE_PLATFORM_WINDOWS_RUNTIME)
	ADD_DEFINITIONS(-ZW)
ENDIF()
IF(KLAYGE_LIBOVR_FOUND)
	ADD_DEFINITIONS(-DKLAYGE_HAVE_LIBOVR)
ENDIF()
IF(MSVC)
	IF(KLAYGE_ARCH_NAME MATCHES "arm")
		SET(CMAKE_EXE_LINKER_FLAGS_DEBUG "${CMAKE_EXE_LINKER_FLAGS_DEBUG} /INCREMENTAL:NO")
		SET(CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO "${CMAKE_EXE_LINKER_FLAGS_RELWITHDEBINFO} /INCREMENTAL:NO")
		SET(CMAKE_SHARED_LINKER_FLAGS_DEBUG "${CMAKE_SHARED_LINKER_FLAGS_DEBUG} /INCREMENTAL:NO")
		SET(CMAKE_SHARED_LINKER_FLAGS_RELWITHDEBINFO "${CMAKE_SHARED_LINKER_FLAGS_RELWITHDEBINFO} /INCREMENTAL:NO")
	ENDIF()
ENDIF()

INCLUDE_DIRECTORIES(${Boost_INCLUDE_DIR})
IF(MSVC)
	IF(MSVC_VERSION LESS 1699)
		INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/DXSDK/Include)
	ENDIF()
ELSE()
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/DXSDK/Include)
ENDIF()
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../KFL/include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/Core/Include)
INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/Plugins/Include)
IF(KLAYGE_PLATFORM_ANDROID)
	INCLUDE_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../External/android_native_app_glue)
ENDIF()
LINK_DIRECTORIES(${Boost_LIBRARY_DIR})
LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/../KFL/lib/${KLAYGE_PLATFORM_NAME})
LINK_DIRECTORIES(${KLAYGE_PROJECT_DIR}/lib/${KLAYGE_PLATFORM_NAME})
IF(KLAYGE_LIBOVR_FOUND)
	INCLUDE_DIRECTORIES(${KLAYGE_LibOVR_PATH}/Include)
	LINK_DIRECTORIES(${KLAYGE_LibOVR_PATH}/Lib/${KLAYGE_VS_PLATFORM_NAME})
ENDIF()

ADD_LIBRARY(${LIB_NAME} ${KLAYGE_PREFERRED_LIB_TYPE}
	${MSGINPUT_IE_SOURCE_FILES} ${MSGINPUT_IE_HEADER_FILES}
)
ADD_DEPENDENCIES(${LIB_NAME} ${KLAYGE_CORELIB_NAME})

IF(MSVC)
	SET(EXTRA_LINKED_LIBRARIES "")
ELSE()
	SET(EXTRA_LINKED_LIBRARIES
		debug KlayGE_Core${KLAYGE_OUTPUT_SUFFIX}_d optimized KlayGE_Core${KLAYGE_OUTPUT_SUFFIX}
		debug KFL${KLAYGE_OUTPUT_SUFFIX}_d optimized KFL${KLAYGE_OUTPUT_SUFFIX}
		${Boost_CHRONO_LIBRARY} ${Boost_SYSTEM_LIBRARY})
ENDIF()
IF(KLAYGE_PLATFORM_WINDOWS_DESKTOP)
	IF(MSVC AND (MSVC_VERSION GREATER 1500))
		SET(EXTRA_LINKED_LIBRARIES ${EXTRA_LINKED_LIBRARIES}
			locationapi sensorsapi)
	ENDIF()
ENDIF()

IF(KLAYGE_LIBOVR_FOUND)
	SET(EXTRA_LINKED_LIBRARIES ${EXTRA_LINKED_LIBRARIES}
		winmm)
	IF(KLAYGE_ARCH_NAME STREQUAL "x86")
		SET(EXTRA_LINKED_LIBRARIES ${EXTRA_LINKED_LIBRARIES}
			debug libovrd optimized libovr)
	ELSE()
		SET(EXTRA_LINKED_LIBRARIES ${EXTRA_LINKED_LIBRARIES}
			debug libovrd64 optimized libovr64)
	ENDIF()
ENDIF()

SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES
	ARCHIVE_OUTPUT_DIRECTORY ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_DEBUG ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELEASE ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_RELWITHDEBINFO ${KLAYGE_OUTPUT_DIR}
	ARCHIVE_OUTPUT_DIRECTORY_MINSIZEREL ${KLAYGE_OUTPUT_DIR}
	PROJECT_LABEL ${LIB_NAME}
	DEBUG_POSTFIX ${CMAKE_DEBUG_POSTFIX}
	OUTPUT_NAME ${LIB_NAME}${KLAYGE_OUTPUT_SUFFIX}
)
IF(KLAYGE_PLATFORM_WINDOWS_RUNTIME)
	SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES VS_WINRT_EXTENSIONS TRUE)
ENDIF()

ADD_PRECOMPILED_HEADER(${LIB_NAME} "KlayGE/KlayGE.hpp" "${KLAYGE_PROJECT_DIR}/Core/Include" "${KLAYGE_PROJECT_DIR}/Plugins/Src/Input/MsgInput/MInputFactory.cpp")

TARGET_LINK_LIBRARIES(${LIB_NAME}
	${EXTRA_LINKED_LIBRARIES}
)

IF(KLAYGE_PREFERRED_LIB_TYPE STREQUAL "SHARED")
	ADD_POST_BUILD(${LIB_NAME} "Input")
 
	INSTALL(TARGETS ${LIB_NAME}
		RUNTIME DESTINATION ${KLAYGE_BIN_DIR}/Input
		LIBRARY DESTINATION ${KLAYGE_BIN_DIR}/Input
		ARCHIVE DESTINATION ${KLAYGE_OUTPUT_DIR}
	)
ENDIF()

SET_TARGET_PROPERTIES(${LIB_NAME} PROPERTIES FOLDER "Input System")
