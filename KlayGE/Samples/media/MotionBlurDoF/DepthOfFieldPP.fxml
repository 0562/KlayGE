<?xml version='1.0'?>

<effect>
	<include name="PostProcess.fxml"/>

	<cbuffer name="per_frame">
		<parameter type="float2" name="focus_plane_inv_range"/>
	</cbuffer>

	<parameter type="texture2D" name="color_tex"/>
	<parameter type="texture2D" name="depth_tex"/>
	<parameter type="texture2D" name="sat_tex"/>

	<parameter type="sampler" name="point_sampler">
		<state name="filtering" value="min_mag_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>

	<shader>
		<![CDATA[
float4 DepthOfFieldBlurFactorPS(float2 tc : TEXCOORD0) : SV_Target
{
	return saturate(abs(depth_tex.Sample(point_sampler, tc).x * focus_plane_inv_range.y + focus_plane_inv_range.x));
}
		]]>
	</shader>

	<technique name="DepthOfFieldBlurFactor">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="PostProcessVS()"/>
			<state name="pixel_shader" value="DepthOfFieldBlurFactorPS()"/>
		</pass>
	</technique>


	<parameter type="float4" name="in_width_height"/>
	<parameter type="float" name="max_radius"/>
	<parameter type="texture2D" name="src_tex"/>

	<shader>
		<![CDATA[
void DepthOfFieldSpreadingVS(float3 pos : POSITION,
						out float4 oClr : TEXCOORD0,
						out float4 oPos : SV_Position)
{
	float2 target_coord = pos.xy * in_width_height.xy;
	float2 original_coord = (target_coord - max_radius * 2) / (in_width_height.xy - max_radius * 4 - 1);
	float radius = floor(saturate(abs(depth_tex.SampleLevel(point_sampler, original_coord, 0).x * focus_plane_inv_range.y + focus_plane_inv_range.x)) * max_radius);
	float area = (radius * 2 + 1) * (radius * 2 + 1);
	
	int index = (int)pos.z;
	float4 offset[4] = { float4(-1, -1, 0, 0), float4(+1, -1, 1, 0), float4(-1, +1, 0, 1), float4(+1, +1, 1, 1) };
	float scale[4] = { +1, -1, -1, +1 };
	oPos = float4((pos.xy + (offset[index].xy * radius + offset[index].zw) * in_width_height.zw) * 2 - 1, 0, 1);
	oPos.y *= KLAYGE_FLIPPING;
	oClr = scale[index] * float4(color_tex.SampleLevel(point_sampler, original_coord, 0).xyz, 1) / area;
}

float4 DepthOfFieldSpreadingPS(float4 clr : TEXCOORD0) : SV_Target
{
	return clr;
}

float4 DepthOfFieldNewSpreadingPS(float2 tc : TEXCOORD0, float4 vpos : SV_Position) : SV_Target
{
	float4 ret = 0;
	
	float2 pos = tc * in_width_height.xy - max_radius * 2;
	float2 original_size = in_width_height.xy - max_radius * 4 - 1;
	
	float4 offset[4] = { float4(+1, +1, 0, 0), float4(-1, +1, -1, 0), float4(+1, -1, 0, -1), float4(-1, -1, -1, -1) };
	float scale[4] = { +1, -1, -1, +1 };
	for (int i = 0; i <= max_radius; ++ i)
	{
		for (int dir = 0; dir < 4; ++ dir)
		{
			float4 o = offset[dir];
			float s = scale[dir];
		
			float2 target_coord = pos + o.xy * i + o.zw;
			float2 original_coord = target_coord / original_size;
			if (all(target_coord >= -max_radius) && all(target_coord < original_size + max_radius))
			{
				float radius = floor(saturate(abs(depth_tex.SampleLevel(point_sampler, original_coord, 0).x * focus_plane_inv_range.y + focus_plane_inv_range.x)) * max_radius);
				if (radius == i)
				{
					float area = (radius * 2 + 1) * (radius * 2 + 1);
					ret += s * float4(color_tex.SampleLevel(point_sampler, original_coord, 0).xyz, 1) / area;
				}
			}
		}
	}
	return ret;
}

void DepthOfFieldNormalizationVS(float4 pos : POSITION,
					out float2 oTex : TEXCOORD0,
					out float4 oPos : SV_Position)
{
	oTex = pos.zw;
	oTex.y = KLAYGE_FLIPPING > 0 ? 1 - oTex.y : oTex.y;
	oPos = float4(pos.xy, 0, 1);
}

float4 DepthOfFieldNormalizationPS(float2 tc : TEXCOORD0) : SV_Target
{
	float4 input = src_tex.Sample(point_sampler, tc);
	return input / input.w;
}
		]]>
	</shader>

	<shader version="4">
		<![CDATA[
struct PS_IN
{
	float4 clr : TEXCOORD0;
	float4 pos : SV_Position;
};

void DepthOfFieldSpreadingVS4(float2 pos : POSITION,
						out float4 oClr : TEXCOORD0,
						out float4 oPos : SV_Position)
{
	float2 target_coord = pos.xy * in_width_height.xy;
	float2 original_coord = (target_coord - max_radius * 2) / (in_width_height.xy - max_radius * 4 - 1);
	float radius = floor(saturate(abs(depth_tex.SampleLevel(point_sampler, original_coord, 0).x * focus_plane_inv_range.y + focus_plane_inv_range.x)) * max_radius);
	float area = (radius * 2 + 1) * (radius * 2 + 1);
	
	oPos = float4(pos.xy, radius, 0);
	oClr = float4(color_tex.SampleLevel(point_sampler, original_coord, 0).xyz, 1) / area;
}

#if KLAYGE_OPENGL
POINT TRIANGLE_OUT void DepthOfFieldSpreadingGS4(AttribArray<float4> in_clr : TEXCOORD0, AttribArray<float4> in_pos : POSITION)
{
#else
[maxvertexcount(4)]
void DepthOfFieldSpreadingGS4(point float4 in_clr[1] : TEXCOORD0, point float4 in_pos[1] : SV_Position, inout PointStream<PS_IN> out_stream)
{
#endif
	static const float4 offset[4] = { float4(-1, -1, 0, 0), float4(+1, -1, 1, 0), float4(-1, +1, 0, 1), float4(+1, +1, 1, 1) };
	static const float scale[4] = { +1, -1, -1, +1 };
	float2 pos = in_pos[0].xy;
	float radius = in_pos[0].z;
	float4 clr = in_clr[0];
	[unroll]
	for (int index = 0; index < 4; ++ index)
	{
		PS_IN gs_out;

		gs_out.pos = float4((pos + (offset[index].xy * radius + offset[index].zw) * in_width_height.zw) * 2 - 1, 0, 1);
		gs_out.pos.y *= KLAYGE_FLIPPING;
		gs_out.clr = scale[index] * clr;
		
#if KLAYGE_OPENGL
		emitVertex(gs_out.clr : TEXCOORD0, gs_out.pos : POSITION);
#else
		out_stream.Append(gs_out);
#endif
	}
}
		]]>
	</shader>

	<macro name="BLOCK_SIZE_X" value="16"/>
	<macro name="BLOCK_SIZE_Y" value="16"/>
	<macro name="MAX_RADIUS" value="8"/>

	<parameter type="rw_texture2D" elem_type="float4" name="spread_tex"/>
	
	<shader type="compute_shader" version="5">
		<![CDATA[
groupshared float2 shared_radius[(BLOCK_SIZE_X + MAX_RADIUS * 2 + 1) * (BLOCK_SIZE_Y + MAX_RADIUS * 2 + 1)];

[numthreads(BLOCK_SIZE_X, BLOCK_SIZE_Y, 1)]
void DepthOfFieldSpreadingCS(uint3 gid : SV_GroupID, 
				uint3 dtid : SV_DispatchThreadID, 
				uint3 gtid : SV_GroupThreadID, 
				uint GI : SV_GroupIndex)
{
	float2 original_size = in_width_height.xy - max_radius * 4 - 1;
	float2 inv_original_size = 1 / original_size;
	const uint shared_width = BLOCK_SIZE_X + MAX_RADIUS * 2 + 1;

	int2 start_coord = gid.xy * int2(BLOCK_SIZE_X, BLOCK_SIZE_Y) - MAX_RADIUS - 1;
	for (uint t = 0; t < (shared_width * shared_width + BLOCK_SIZE_X * BLOCK_SIZE_Y - 1) / (BLOCK_SIZE_X * BLOCK_SIZE_Y); ++ t)
	{
		uint index = GI + t * (BLOCK_SIZE_X * BLOCK_SIZE_Y);
		if (index < shared_width * shared_width)
		{
			uint y = index / shared_width;
			uint x = index - y * shared_width;
			int2 addr = start_coord - 2 * max_radius + int2(x, y);
			if (all(addr >= -max_radius) && all(addr < original_size + max_radius))
			{
				float2 original_coord = (addr + 0.5f) * inv_original_size;
				float radius = floor(saturate(abs(depth_tex.SampleLevel(point_sampler, original_coord, 0).x * focus_plane_inv_range.y + focus_plane_inv_range.x)) * max_radius);
				float area = (radius * 2 + 1) * (radius * 2 + 1);
				shared_radius[y * shared_width + x] = float2(radius, 1 / area);
			}
			else
			{
				shared_radius[y * shared_width + x] = float2(max_radius + 1, 0);
			}
		}
	}
	GroupMemoryBarrierWithGroupSync();

	float4 ret = 0;
	
	float4 offset[4] = { float4(+1, +1, 0, 0), float4(-1, +1, -1, 0), float4(+1, -1, 0, -1), float4(-1, -1, -1, -1) };
	float scale[4] = { +1, -1, -1, +1 };
	float2 center_coord = gtid.xy + MAX_RADIUS + 1;
	float2 global_center_coord = dtid.xy - max_radius * 2 + 0.5f;
	for (int i = 0; i <= max_radius; ++ i)
	{
		for (int dir = 0; dir < 4; ++ dir)
		{
			float4 o = offset[dir];
			float s = scale[dir];

			float2 offset = o.xy * i + o.zw;
			float2 target_coord = center_coord + offset;
			float2 radius = shared_radius[target_coord.y * shared_width + target_coord.x];
			if (radius.x == i)
			{
				float2 original_coord = (global_center_coord + offset) * inv_original_size;
				ret += s * float4(color_tex.SampleLevel(point_sampler, original_coord, 0).xyz, 1) * radius.y;
			}
		}
	}
	
	spread_tex[dtid.xy] = ret;
}
		]]>
	</shader>
	
	<technique name="DepthOfFieldSpreading">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="blend_enable" value="true"/>
			<state name="src_blend" value="one"/>
			<state name="dest_blend" value="one"/>
			<state name="src_blend_alpha" value="one"/>
			<state name="dest_blend_alpha" value="one"/>

			<state name="vertex_shader" value="DepthOfFieldSpreadingVS()"/>
			<state name="pixel_shader" value="DepthOfFieldSpreadingPS()"/>
		</pass>
	</technique>

	<technique name="DepthOfFieldSpreading4">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="blend_enable" value="true"/>
			<state name="src_blend" value="one"/>
			<state name="dest_blend" value="one"/>
			<state name="src_blend_alpha" value="one"/>
			<state name="dest_blend_alpha" value="one"/>

			<state name="vertex_shader" value="DepthOfFieldSpreadingVS4()"/>
			<state name="geometry_shader" value="DepthOfFieldSpreadingGS4()"/>
			<state name="pixel_shader" value="DepthOfFieldSpreadingPS()"/>
		</pass>
	</technique>

	<technique name="DepthOfFieldNewSpreading">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="PostProcessVS()"/>
			<state name="pixel_shader" value="DepthOfFieldNewSpreadingPS()"/>
		</pass>
	</technique>

	<technique name="DepthOfFieldSpreadingCS">
		<pass name="p0">
			<state name="compute_shader" value="DepthOfFieldSpreadingCS()"/>
		</pass>
	</technique>

	<technique name="DepthOfFieldNormalization">
		<pass name="p0">
			<state name="depth_enable" value="false"/>
			<state name="depth_write_mask" value="0"/>

			<state name="vertex_shader" value="DepthOfFieldNormalizationVS()"/>
			<state name="pixel_shader" value="DepthOfFieldNormalizationPS()"/>
		</pass>
	</technique>
</effect>
