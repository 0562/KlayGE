<?xml version='1.0'?>

<effect>
	<parameter type="float4x4" name="model_view_proj"/>
	<parameter type="float4x4" name="model"/>
	<parameter type="float4x4" name="obj_model_to_light_model"/>

	<shader>
		<![CDATA[
void GenShadowMapVS(float4 Position : POSITION,
						float3 Normal   : NORMAL,
						out float oDist : TEXCOORD0,
						out float4 oPos : SV_Position)
{
    oPos = mul(Position, model_view_proj);
    float4 LightWorldPos = mul(Position, obj_model_to_light_model);
    LightWorldPos /= LightWorldPos.w;
	oDist = length(LightWorldPos.xyz);
}

float4 GenShadowMapPS(float dist : TEXCOORD0) : SV_Target
{
	float2 dxdy = float2(ddx(dist), ddy(dist));
	return float4(dist, dist * dist + 0.25f * dot(dxdy, dxdy), 0, 1);
}
		]]>
	</shader>

	<technique name="GenShadowMap">
		<pass name="p0">
			<state name="cull_mode" value="none"/>

			<state name="vertex_shader" value="GenShadowMapVS()"/>
			<state name="pixel_shader" value="GenShadowMapPS()"/>
		</pass>
	</technique>

	<parameter type="float" name="min_variance"/>
	<parameter type="float" name="bleeding_reduce"/>
	<parameter type="float3" name="light_pos"/>
	<parameter type="int" name="flipping"/>

	<parameter type="textureCUBE" name="lamp_tex"/>
	<parameter type="sampler" name="lamp_sampler">
		<state name="filtering" value="min_mag_linear_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>
	
	<parameter type="textureCUBE" name="shadow_cube_tex"/>
	<parameter type="sampler" name="shadow_sampler">
		<state name="filtering" value="min_mag_linear_mip_point"/>
		<state name="address_u" value="clamp"/>
		<state name="address_v" value="clamp"/>
	</parameter>

	<shader>
		<![CDATA[
void RenderSceneVS(float4 Position : POSITION,
					float3 Normal   : NORMAL,
					out float3 oDiffuse : COLOR0,
					out float4 oLightWorldPos : TEXCOORD0,
					out float4 oPos : SV_Position)
{
	float4 world_pos = mul(Position, model);
	float3 world_normal = normalize(mul(Normal, (float3x3)model));

	oPos = mul(Position, model_view_proj);
	oLightWorldPos = mul(Position, obj_model_to_light_model);

	world_pos /= world_pos.w;
	oDiffuse = dot(normalize(light_pos - world_pos.xyz), world_normal);
}

float linstep(float min, float max, float v)
{
	return clamp((v - min) / (max - min), 0, 1);
}

float4 RenderScenePS(float3 diffuse : COLOR0,
				float4 LightWorldPos : TEXCOORD0) : SV_Target 
{
	float3 ori_color = diffuse * lamp_tex.Sample(lamp_sampler, LightWorldPos.xyz).rgb;
	float3 shadow_dir = float3(LightWorldPos.x, flipping * LightWorldPos.y, LightWorldPos.z);
	float2 moments = shadow_cube_tex.Sample(shadow_sampler, shadow_dir).xy;
	
	LightWorldPos /= LightWorldPos.w;
	float dist_sq = dot(LightWorldPos.xyz, LightWorldPos.xyz);
	float dist = sqrt(dist_sq);

	float f = 1 / (0.01f + 0.5f * dist_sq);

	float p = (dist <= moments.x);
	// Variance shadow mapping
	float variance = moments.y - moments.x * moments.x;
	variance = max(variance, min_variance);
	float m_d = moments.x - dist;
	float p_max = variance / (variance + m_d * m_d);
	p_max = linstep(bleeding_reduce, 1, p_max);

	f *= max(p, p_max);
	
	return float4(ori_color * f, 1.0);
}
		]]>
	</shader>

	<technique name="RenderScene">
		<pass name="p0">
			<state name="cull_mode" value="back"/>

			<state name="vertex_shader" value="RenderSceneVS()"/>
			<state name="pixel_shader" value="RenderScenePS()"/>
		</pass>
	</technique>
</effect>
